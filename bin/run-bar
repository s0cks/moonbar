#!/usr/bin/env zsh
MBAR_EXEC="${MBAR_EXEC:-./moonbar}"
MBAR_LIB_HOME="${MBAR_LIB_HOME:-${0:a:h:h}}"
MBAR_CLIB_HOME="${MBAR_CLIB_HOME:-$(pwd)/bindings}"
MBAR_CONFIG_HOME="${MBAR_CONFIG_HOME:-$XDG_CONFIG_HOME/bar}"
if [[ ! (-f "$MBAR_EXEC" && -x "$MBAR_EXEC") ]]; then
  echo "failed to find moonbar executable: $MBAR_EXEC"
  ls -lash .
  return 1
fi

__generate_lua_path() {
  lua_lib_paths=(
    "$MBAR_LIB_HOME"
  )
  reply=()
  for path_dir in $lua_lib_paths; do
    reply+=(
      "$path_dir/?.lua"
      "$path_dir/lua/?.lua"
    )
  done
  return 0
}

__generate_lua_cpath() {
  bindings=$(
    fselect abspath from $MBAR_CLIB_HOME/ depth 1 \
      where is_dir and name != 'CMakeFiles'
  )
  bindings=("${(f)bindings}")

  reply=()
  for binding_dir in $bindings; do
    reply+=(
      "$binding_dir/?.so"
    )
  done
  return 0
}

__generate_lua_path
lua_path="${(j|;|)reply};;"

__generate_lua_cpath
lua_cpath="${(j|;|)reply};;"

(cd "$MBAR_CONFIG_HOME/" \
  && MESA_DEBUG=silent \
     MESA_LOG_FILE=mesa.log \
     LUA_PATH=$lua_path \
     LUA_CPATH=$lua_cpath \
     $MBAR_EXEC)
